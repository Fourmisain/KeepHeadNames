plugins {
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id "dev.architectury.loom" version "1.3-SNAPSHOT" apply false
}

architectury {
	minecraft = project.minecraft_version
}

subprojects {
	apply plugin: "dev.architectury.loom"

	dependencies {
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	}

	version = "${project.mod_version}-${project.name}-${project.minecraft_version}"

	processResources {
		// expand every property from gradle.properties
		def stringProperties = providers.gradlePropertiesPrefixedBy('').get()
		stringProperties.each { inputs.property(it.key, it.value) }

		["${project.mod_id}.mixins.json", "fabric.mod.json", "META-INF/mods.toml"].each {
			filesMatching(it) {
				expand(stringProperties) {
					escapeBackslash = true
				}
			}
		}
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architectury-plugin"

	// version = project.mod_version
	group = project.maven_group

	base {
		archivesName = project.archives_base_name
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = project.java_version as Integer
	}
}
